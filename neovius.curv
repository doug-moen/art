// Copyright 2018 Doug Moen
// Licensed under the GNU Affero General Public License v3.0
// See https://www.gnu.org/licenses/agpl-3.0.en.html

parametric {
   Smoothing :: slider(0,2) = .4;
   Offset :: slider(0,3) = 1.2;
   Shell :: slider(0,3) = 1.2;
}
let
// The Neovius surface: https://en.wikipedia.org/wiki/Neovius_surface
neovius = make_shape {
    dist(x,y,z,t): -(3*(cos x+cos y+cos z) + 4*cos x*cos y*cos z),
    is_3d: true
};
rcube r d = cube(d-2*r) >> offset r;
rcere r d = morph .5 (sphere d, rcube r d);

in
neovius >> offset Offset >> shell Shell >> lipschitz 10
`smooth Smoothing .intersection` rcube 2.2 18.2
`difference` repeat_xyz (8,8,8) (sphere 2)  // remove detritus
>> scale 4   // a nice size for 3D printing
